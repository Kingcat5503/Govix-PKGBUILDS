pkgname=govix-calamares
_pkgname=calamares
pkgver=3.4.0
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
url="https://github.com/calamares/calamares/"
license=('LGPL')
depends=(
	'boost-libs'
	'ckbcomp'
	'cryptsetup'
	'doxygen'
	'efibootmgr'
	'gptfdisk'
	'gtk-update-icon-cache'
	'hwinfo'
	'icu'
	'kconfig'
	'kcoreaddons'
	'kcrash'
	'ki18n'
	'kparts'
	'kpmcore'
	'kservice'
	'kwidgetsaddons'
	'libpwquality'
	'mkinitcpio-openswap'
	'polkit-qt6'
	'rsync'
	'qt6-declarative'
	'solid'
	'squashfs-tools'
	'yaml-cpp'
)
makedepends=(
	'boost'
	'cmake'
	'extra-cmake-modules'
	'ninja'
	'python-jsonschema'
	'python-pyaml'
	'python-unidecode'
	'qt6-tools'
)
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

## Use a local tarball instead of a git checkout. Place the tarball next to this PKGBUILD
## e.g. PKGBUILDS/calamares/calamares-${pkgver}.tar.gz
source=("calamares-${pkgver}.tar.gz"
	"calamares.desktop"
	"calamares_polkit"
	"49-nopasswd-calamares.rules")

sha256sums=(
	'd48eaf946e19ee62a3c2b5c204bb1b2f6d3b16349f6fecf03eeb725fe2e32b06'
	'5a58ba50dc8280e706cc3bb6bc9769f69f6cbacbe553a3aee0806ee9963da4b0'
	'c176b28007bd1c1f23d8dbb2c936fa54d0c01bacfb67290ddad597606c129df3'
	'56d85ff6bf860b9559b8c9f997ad9b1002f3fccc782073760eca505e3bddd176'
)

prepare() {
	# Work in the extracted source directory which expands to calamares-${pkgver}
	cd "${srcdir}/$_pkgname-${pkgver}"
	sed -i -e 's/etc\/sddm.conf/etc\/sddm.conf.d\/kde_settings.conf/' src/modules/displaymanager/main.py

	# change version
	_ver="$(cat CMakeLists.txt | grep -m3 -e "  VERSION" | grep -o "[[:digit:]]*" | xargs | sed s'/ /./g')"
	sed -i -e "s|CALAMARES_VERSION [0-9.]\+|CALAMARES_VERSION $pkgver|g" CMakeLists.txt

}

build() {
	cd "${srcdir}/$_pkgname-${pkgver}"

  cmake -S . -Bbuild \
      -GNinja \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DWITH_APPSTREAM=OFF \
      -DWITH_PYBIND11=OFF \
      -DWITH_QT6=ON \
      -DSKIP_MODULES="dracut \
          dracutlukscfg \
          dummycpp \
          dummyprocess \
          dummypython \
          dummypythonqt \
          initramfs \
          initramfscfg \
          interactiveterminal \
          keyboardq \
          license \
          localeq \
          oemid \
          packagechooserq \
          partitionq \
          services-openrc \
          summaryq \
          tracking \
          usersq \
          welcomeq \
          zfs \
          zfshostid"

  cmake --build build
}
package() {
	cd "${srcdir}/$_pkgname-${pkgver}/build"
	DESTDIR="${pkgdir}" cmake --build . --target install
	install -Dm644 "${srcdir}/calamares.desktop" "$pkgdir/etc/xdg/autostart/calamares.desktop"
	install -Dm755 "${srcdir}/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
	chmod 750 "$pkgdir"/etc/polkit-1/rules.d
}
